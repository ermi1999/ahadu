ክፍል ሳጥን {
    ማስጀመሪያ(መረጃ) {
        ይህ.መረጃ = መረጃ;
        ይህ.ቀጣይ = ባዶ;
    }
}

ክፍል የተያያዘ_ዘርፍ {
    ማስጀመሪያ() {
        ይህ.አናት = ባዶ;
    }

    መጀመሪያ_ላይ_አስገባ(መረጃ) {
        መለያ አዲስ_ሳጥን = ሳጥን(መረጃ);
        ከሆነ (ይህ.አናት == ባዶ) {
            ይህ.አናት = አዲስ_ሳጥን;
            መልስ;
        } ካልሆነ {
            አዲስ_ሳጥን.ቀጣይ = ይህ.አናት;
            ይህ.አናት = አዲስ_ሳጥን;
        }
    }

    በጠቋሚው_ቦታ_ላይ_አስገባ(መረጃ, ጠቋሚ) {
        መለያ አዲስ_ሳጥን = ሳጥን(መረጃ);
        መለያ አሁን_ላይ_ያለው_ሳጥን = ይህ.አናት;
        መለያ ቦታ = 0;
        ከሆነ (ቦታ == ጠቋሚ) {
            ይህ.መጀመሪያ_ላይ_አስገባ(መረጃ);
        } ካልሆነ {
            እስከ (አሁን_ላይ_ያለው_ሳጥን != ባዶ  እና  ቦታ + 1 != ጠቋሚ) {
                ቦታ = ቦታ + 1;
                አሁን_ላይ_ያለው_ሳጥን = አሁን_ላይ_ያለው_ሳጥን.ቀጣይ;
            }
            ከሆነ (አሁን_ላይ_ያለው_ሳጥን != ባዶ) {
                አዲስ_ሳጥን.ቀጣይ = አሁን_ላይ_ያለው_ሳጥን.ቀጣይ;
                አሁን_ላይ_ያለው_ሳጥን.ቀጣይ = አዲስ_ሳጥን;
            } ካልሆነ አውጣ "ቦታው አልተገኘም።";
        }
    }

    መጨረሻ_ላይ_አስገባ(መረጃ) {
        መለያ አዲስ_ሳጥን = ሳጥን(መረጃ);
        ከሆነ (ይህ.አናት == ባዶ) {
            ይህ.አናት = አዲስ_ሳጥን;
            መልስ;
        }
        
        መለያ አሁን_ላይ_ያለው_ሳጥን = ይህ.አናት;
        እስከ (አሁን_ላይ_ያለው_ሳጥን.ቀጣይ != ባዶ) አሁን_ላይ_ያለው_ሳጥን = አሁን_ላይ_ያለው_ሳጥን.ቀጣይ;

        አሁን_ላይ_ያለው_ሳጥን.ቀጣይ = አዲስ_ሳጥን;
    }

    ዘርፍ_አውጣ() {
        መለያ አሁን_ላይ_ያለው_ሳጥን = ይህ.አናት;
        እስከ (አሁን_ላይ_ያለው_ሳጥን.ቀጣይ != ባዶ) {
            አውጣ አሁን_ላይ_ያለው_ሳጥን.መረጃ;
            አሁን_ላይ_ያለው_ሳጥን = አሁን_ላይ_ያለው_ሳጥን.ቀጣይ;
        }
    }
}

መለያ ዘርፍ = የተያያዘ_ዘርፍ();

ዘርፍ.መጀመሪያ_ላይ_አስገባ("ሀ");
ዘርፍ.መጨረሻ_ላይ_አስገባ("ለ");
ዘርፍ.መጨረሻ_ላይ_አስገባ("ሐ");
ዘርፍ.መጨረሻ_ላይ_አስገባ("መ");

አውጣ "ዘርፍ";
ዘርፍ.ዘርፍ_አውጣ();